buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.+'
    }
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'trust.nccgroup'
version = '1.0.0'

description = """Human-friendly Thrift encoder/decoder"""

sourceCompatibility = 1.7
targetCompatibility = 1.7


repositories {
    mavenCentral()
}

dependencies {
    api group: 'org.apache.thrift', name: 'libthrift', version:'0.9.3'
    api group: 'org.json', name: 'json', version:'20160810'

    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version:'1.4.0'
    testImplementation group: 'junit', name: 'junit', version:'4.12'
}

jar {
    manifest {
        attributes (
                "Main-Class": "trust.nccgroup.readablethrift.InteractiveCodec",
                "Manifest-Version": "1.0",
        )
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Readable Thrift'
                packaging 'jar'
                description description
                url 'https://github.com/nccgroup/readable-thrift'
                scm {
                    url 'https://github.com/nccgroup/readable-thrift'
                    connection 'scm:git:git://github.com/nccgroup/readable-thrift.git'
                    developerConnection 'scm:git:ssh:git@github.com:nccgroup/readable-thrift.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'jamchamb'
                        name 'James Chambers'
                        email 'james.chambers@nccgroup.trust'
                    }
                }
            }
        }
    }
}

defaultTasks 'shadowJar'
